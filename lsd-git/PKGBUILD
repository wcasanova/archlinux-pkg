# Maintainer: Peltoche <dev@halium.fr>
# Co-maintainer: Andrea Pascal <andrea@anodium.net>
# Maintainer: Walter Casanova - "Y29udGFjdG9Ad2FsdGVyY2FzYW5vdmEudGVjaAo=" - base64

#indent = tab
#tab-size = 4

pkgname="lsd-git"
_pkgname="lsd"
pkgver=0.23.1.976.gea56a7c
pkgrel=1
pkgdesc="The next gen ls command"
arch=("i686" "x86_64" "arm" "aarch64")
url="https://github.com/Peltoche/lsd"
license=("MIT")
depends=()
optdepends=('powerline-fonts: powerline symbols for terminals'
			'awesome-terminal-fonts'
			)
makedepends=("rust" "pandoc" "cargo" "git")
provides=("lsd")
conflicts=("lsd")
source=("$_pkgname::git+https://github.com/Peltoche/lsd.git")
sha256sums=("SKIP")

pkgver() {
	cd "$_pkgname"
	echo "$(grep '^version =' Cargo.toml|head -n1|cut -d\" -f2).$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
	cd ${_pkgname}
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "$_pkgname"
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	export SHELL_COMPLETIONS_DIR="$PWD/completions"
	cargo build  --release --frozen --all-features
	pandoc "doc/$_pkgname.md" --standalone --to=man -o "doc/$_pkgname.1"
}

#check() {
#	cd "$_pkgname"
#	export RUSTUP_TOOLCHAIN=stable
#	cargo test --frozen --all-features
#}

package() {
	cd "$srcdir/$_pkgname"
	install -Dm755 target/release/$_pkgname "$pkgdir/usr/bin/$_pkgname"
	install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
	install -Dm644 completions/_lsd "$pkgdir/usr/share/zsh/site-functions/_lsd"
	install -Dm644 completions/lsd.bash "$pkgdir/usr/share/bash-completion/completions/lsd"
	install -Dm644 completions/lsd.fish "$pkgdir/usr/share/fish/vendor_completions.d/lsd.fish"
	install -Dm 644 "doc/$_pkgname.1" -t "$pkgdir/usr/share/man/man1"
}
