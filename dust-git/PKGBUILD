#Maintainer: Walter Casanova - "Y29udGFjdG9Ad2FsdGVyY2FzYW5vdmEudGVjaAo=" - base64
# Maintainer: Caleb Maclennan <caleb@alerque.com>

#indent = tab
#tab-size = 4

pkgname=dust-git
_pkgname=${pkgname%-git}
pkgver=0.8.4.r3.g7d13fe9
pkgrel=1
pkgdesc="A more intuitive version of du in rust"
arch=('x86_64' 'i686')
url="https://github.com/bootandy/dust"
license=('Apache')
makedepends=('cargo' 'git')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
source=("$_pkgname::git+$url.git")
sha256sums=('SKIP')

prepare() {
	cd "$_pkgname"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

pkgver() {
	cd "$_pkgname"
	git describe --long --tags --abbrev=7 --match='v[0-9]*' |
	sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	cd $_pkgname
	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	cargo build --frozen --release --all-features
}

check() {
	cd $_pkgname
	export RUSTUP_TOOLCHAIN=stable
	cargo test --frozen --all-features
}

package() {
	cd "$_pkgname"
	install -Dm755 -t "$pkgdir/usr/bin/" "target/release/$_pkgname"
	install -Dm644 -t "$pkgdir/usr/share/licenses/$pkgname/" LICENSE
	install -Dm644 -t "$pkgdir/usr/share/doc/$pkgname/" README.md
}
